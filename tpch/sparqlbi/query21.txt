define sql:signal-void-variables 1
prefix rdfh: <http://lod2.eu/schemas/rdfh#>

select
    ?supp+>rdfh:s_name,
    (count(1)) as ?numwait
where {
         ?supp a rdfh:supplier .
         ?supp+>rdfh:s_has_nation rdfh:n_name "%NATION%" .
         ?li1 a rdfh:lineitem ; rdfh:l_has_supplier ?supp ; rdfh:l_has_order ?ord .
         ?ord rdfh:o_orderstatus "F" .
         filter ( ?li1+>rdfh:l_receiptdate > ?li1+>rdfh:l_commitdate
          && bif:exists ((select (1)
                          where {
                            ?li2 rdfh:l_has_order ?ord .
                            ?li2 rdfh:l_has_supplier ?supp2 .
                            filter (?supp != ?supp2) }))
          && !bif:exists ((select (1)
                          where {
                            ?li3 rdfh:l_has_order ?ord .
                            ?li3 rdfh:l_has_supplier ?supp3 .
                            filter (
                              ?li3+>rdfh:l_receiptdate > ?li3+>rdfh:l_commitdate &&
                              ?supp3 != ?supp) 
                          }))
         )
       }
order by
    desc (count(1))
    ?supp+>rdfh:s_name
limit 100
