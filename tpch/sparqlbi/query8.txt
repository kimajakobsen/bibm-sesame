define sql:signal-void-variables 1
prefix rdfh: <http://lod2.eu/schemas/rdfh#>

select
  ?o_year,
  (?sum1 / ?sum2) as ?mkt_share
where {
    { select
        ?o_year
        sum (?volume * bif:equ (?nation, "%NATION%")) as ?sum1
        sum (?volume) as ?sum2
      where {
          { select
              (bif:year (?ord+>rdfh:o_orderdate)) as ?o_year,
              (?li+>rdfh:l_lineextendedprice * (1 - ?li+>rdfh:l_linediscount)) as ?volume,
              ?n2+>rdfh:n_name as ?nation
            where {
                ?li a rdfh:lineitem ; rdfh:l_has_order ?ord ; rdfh:l_has_part ?part .
                ?li+>rdfh:l_has_supplier rdfh:s_has_nation ?n2 .
                ?ord+>rdfh:o_has_customer+>rdfh:c_has_nation+>rdfh:n_has_region rdfh:r_name "%REGION%" .
                ?part rdfh:p_type "%TYPE%" .
                filter ((?ord+>rdfh:o_orderdate >= "1995-01-01"^^xsd:date) &&
                  (?ord+>rdfh:o_orderdate <= "1996-12-31"^^xsd:date) ) } } } } }
order by
  ?o_year
