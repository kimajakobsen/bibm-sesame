define sql:signal-void-variables 1
prefix rdfh: <http://lod2.eu/schemas/rdfh#>

select
  ?supplier+>rdfh:s_suppkey 
  ?supplier+>rdfh:s_name 
  ?supplier+>rdfh:s_address 
  ?supplier+>rdfh:s_phone
  ?total_revenue
where
  {
    ?supplier a rdfh:supplier .
      {
        select
          ?supplier
          (sum(?l_extendedprice * (1 - ?l_discount))) as ?total_revenue
        where
          {
            [ a rdfh:lineitem ;
              rdfh:l_shipdate ?l_shipdate ;
              rdfh:l_lineextendedprice ?l_extendedprice ;
              rdfh:l_linediscount ?l_discount ;
              rdfh:l_has_supplier ?supplier ] .
            filter (
                ?l_shipdate >= "1996-01-01"^^xsd:date and
                ?l_shipdate < bif:dateadd ("month", 3, "1996-01-01"^^xsd:date) )
          }
      }
      {
        select max (?l2_total_revenue) as ?maxtotal
        where
          {
              {
                select
                  ?supplier2
                  (sum(?l2_extendedprice * (1 - ?l2_discount))) as ?l2_total_revenue
                where
                  {
                    [ a rdfh:lineitem ;
                      rdfh:l_shipdate ?l2_shipdate ;
                      rdfh:l_lineextendedprice ?l2_extendedprice ;
                      rdfh:l_linediscount ?l2_discount ;
                      rdfh:l_has_supplier ?supplier2 ] .
                    filter (
                        ?l2_shipdate >= "%MONTH%-01"^^xsd:date and
                        ?l2_shipdate < bif:dateadd ("month", 3, "%MONTH%-01"^^xsd:date) )
                  }
              }
          }
      }
    filter (?total_revenue = ?maxtotal)
  }
order by
  ?supplier
