define sql:signal-void-variables 1
prefix rdfh: <http://lod2.eu/schemas/rdfh#>

select
  ?o_orderkey,
  sum(?l_lineextendedprice*(1 - ?l_linediscount)) as ?revenue,
  ?o_orderdate,
  ?o_shippriority
where  {
  ?li a rdfh:lineitem ;
	rdfh:l_lineextendedprice ?l_lineextendedprice ;
    rdfh:l_linediscount ?l_linediscount ;
    rdfh:l_has_order ?ord ;
    rdfh:l_shipdate ?l_shipdate .
  ?ord a rdfh:orders ;
	rdfh:o_orderdate ?o_orderdate ;
    rdfh:o_shippriority ?o_shippriority ;
    rdfh:o_orderkey ?o_orderkey ;
    rdfh:o_has_customer ?cust .
  ?cust a rdfh:customer ;
    rdfh:c_mktsegment "%SEGMENT%" .
  filter ((?o_orderdate < "%DATE%"^^xsd:date) &&
    (?l_shipdate > "%DATE%"^^xsd:date) ) 
}
group by
  ?o_orderkey
  ?o_orderdate
  ?o_shippriority
order by
  desc (sum (?l_lineextendedprice * (1 - ?l_linediscount)))
  ?o_orderdate
limit 10
