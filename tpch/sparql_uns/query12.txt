define sql:signal-void-variables 1
prefix rdfh: <http://lod2.eu/schemas/rdfh#>

select
  ?l_shipmode,
  sum (
    bif:__or (
      bif:equ (?o_orderpriority, "1-URGENT"),
      bif:equ (?o_orderpriority, "2-HIGH") ) ) as ?high_line_count,
  sum (1 -
    bif:__or (
      bif:equ (?o_orderpriority, "1-URGENT"),
      bif:equ (?o_orderpriority, "2-HIGH") ) ) as ?low_line_count
where  {
    ?li a rdfh:lineitem ;
       rdfh:has_order ?ord ;
       rdfh:commitdate ?l_commitdate ;
       rdfh:receiptdate ?l_receiptdate ;
       rdfh:shipmode ?l_shipmode ;
       rdfh:shipdate ?l_shipdate .
    ?ord rdfh:orderpriority ?o_orderpriority .
    filter (?l_shipmode in ("%SHIPMODE1%", "%SHIPMODE2%") &&
      (?l_commitdate < ?l_receiptdate) &&
      (?l_shipdate < ?l_commitdate) &&
      (?l_receiptdate >= "%YEAR%-01-01"^^xsd:date) &&
      (?l_receiptdate < bif:dateadd ("year", 1, "%YEAR%-01-01"^^xsd:date)) )
  }
group by
  ?l_shipmode
order by
  ?l_shipmode
