define sql:signal-void-variables 1
prefix rdfh: <http://lod2.eu/schemas/rdfh#>

select
  (bif:LEFT (?cust+>rdfh:phone, 2)) as ?cntrycode,
  (count (1)) as ?numcust,
  sum (?cust+>rdfh:acctbal) as ?totacctbal
where {
    ?cust a rdfh:customer .
    filter (
      bif:LEFT (?cust+>rdfh:phone, 2) in (%COUNTRY_CODE_SET%) &&
      (?cust+>rdfh:acctbal >
        ( select (avg (?cust2+>rdfh:acctbal)) as ?acctbal_threshold
          where
            {
              ?cust2 a rdfh:customer .
              filter ((?cust2+>rdfh:acctbal > 0.00) &&
                bif:LEFT (?cust2+>rdfh:phone, 2) in (%COUNTRY_CODE_SET%) )
            } ) ) &&
      !bif:exists (
        ( select (1)
          where { ?ord rdfh:has_customer ?cust } ) ) )
  }
group by (bif:LEFT (?cust+>rdfh:phone, 2))
order by (bif:LEFT (?cust+>rdfh:phone, 2))
